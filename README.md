# conman

Luminus database connection management and SQL query generation library

The library provides pooled connections using the [HikariCP](https://github.com/brettwooldridge/HikariCP) library.

The queries are generated using [HugSQL](https://github.com/layerware/hugsql) and wrapped with
connection aware functions.

## Usage

[![Clojars Project](http://clojars.org/conman/latest-version.svg)](http://clojars.org/conman)

Conman relies on a dynamic variable to manage the connection. The dynamic variable allows the connection to be
rebound contextually for transactions. When working with multiple databases, a separate var is required
to track each database connection.

### Defining Queries

SQL statements should be populated in files that are accessible on the resource path.
For example, we could create a file `resources/sql/queries.sql` with
the following content:

``` sql
-- :name create-user! :! :n
-- creates a new user record
INSERT INTO users
(id, first_name, last_name, email, pass)
VALUES (:id, :first_name, :last_name, :email, :pass)

-- :name get-user :? :1
-- retrieve a user given the id.
SELECT * FROM users
WHERE id = :id

-- :name get-all-users :? :*
-- retrieve all users.
SELECT * FROM users
```
See the official [HugSQL docs](http://www.hugsql.org/) for further examples.

The queries are bound to the connection using the `bind-connection` macro. This macro
accepts the connection var followed by one or more strings representing SQL query files.

The lifecycle of the connection is expected to be managed using a library such as [mount](https://github.com/tolitius/mount).


```clojure
(ns myapp.db
  (:require [mount.core :as mount]
            [conman.core :as conman]))

(def pool-spec
  {:adapter    :postgresql
   :init-size  1
   :min-idle   1
   :max-idle   4
   :max-active 32
   :jdbc-url "jdbc:postgresql://localhost/myapp?user=user&password=pass"})

(defstate ^:dynamic *db*
          :start (conman/connect! pool-spec)
          :stop (conman/disconnect! *db*))

(conman/bind-connection *db* "sql/queries.sql")
```

The `bind-connection` generates `create-user!` and `get-user` functions
in the current namespace. These functions can be called in four different ways:

```clojure
;; when called with no argument then the HugSQL generated function
;; will be called with an empty parameter map and the connection specified in the *db* var
(get-all-users)

;; when a parameter map is passed as the argument, then the map and the connection specified
;; in the *db* var will be passed to the HugSQL generated function
(create-user! {:id "foo" :first_name "Bob" :last_name "Bobberton" :email nil :pass nil})

;; an explicit connection and a parameter map can be
;; passed to the function
(get-user some-other-conn {:id "foo"})

;; finally, an explicit connection and a parameter map, options, and optional command options
;; can be passed to the function
(get-user some-other-conn {:id "foo"} opts)
(get-user some-other-conn {:id "foo"} opts cmd-opt1 cmd-opt2)

```

Next, the `connect!` function should be called to initialize the database connection.
The function accepts a map with the database specification.

```clojure
(def pool-spec
  {:adapter    :postgresql
   :jdbc-url   "jdbc:postgresql://localhost/myapp?user=user&password=pass"
   :init-size  1
   :min-idle   1
   :max-idle   4
   :max-active 32})

(connect! pool-spec)
```

The following options are supported:

* `:jdbc-url`
* `:datasource`
* `:datasource-classname`
* `:username`
* `:password`
* `:auto-commit?`
* `:conn-timeout`
* `:idle-timeout`
* `:max-lifetime`
* `:min-idle`
* `:max-pool-size`
* `:pool-name`

The connection can be terminated by running the `disconnect!` function:

```clojure
(disconnect! conn)
```

A connection can be reset using the `reconnect!` function:

```clojure
(reconnect! conn pool-spec)
```

When using a dynamic connection, it's possible to use the `with-transaction`
macro to rebind it to the transaction connection. The SQL query functions
generated by the `bind-connection` macro will automatically use the transaction
connection in that case:

```clojure
(with-transaction [conn]
  (jdbc/db-set-rollback-only! conn)
  (create-user!
    {:id         "foo"
     :first_name "Sam"
     :last_name  "Smith"
     :email      "sam.smith@example.com"})
  (get-user {:id "foo"}))
```

The isolation level and readonly status of the transaction may be specified using the `:isolation`
and `:read-only?` keys respectively:

```clojure
(with-transaction
    [conn :isolation :serializable]
    (= java.sql.Connection/TRANSACTION_SERIALIZABLE
       (.getTransactionIsolation (sql/db-connection conn))))
(with-transaction
  [conn :isolation :read-uncommitted]
  (= java.sql.Connection/TRANSACTION_READ_UNCOMMITTED
     (.getTransactionIsolation (sql/db-connection conn))))
```

## License

Copyright Â© 2015 Dmitri Sotnikov and Carousel Apps Ltd.

Distributed under the Eclipse Public License either version 1.0 or (at
your option) any later version.
